#############################################################################
 #
 #  This file is part of Verkko, a software program that assembles
 #  whole-genome sequencing reads into telomere-to-telomere
 #  haplotype-resolved chromosomes.
 #
 #  Except as indicated otherwise, this is a 'United States Government
 #  Work', and is released in the public domain.
 #
 #  File 'README.licenses' in the root directory of this distribution
 #  contains full conditions and disclaimers.
 #
 ##

#
#  Rule buildPackages creates input packages for Canu's consensus module.
#

checkpoint buildPackages:
    input:
        hifi        = HIFI_READS,
        layout      = {rules.layoutContigs.output.layout},
        ont_subset  = {rules.extractONT.output.ont_subset}    if config['withONT'] == "True" else {rules.emptyfile.output},
        ont_id_file = {rules.extractONT.output.ont_subset_id} if config['withONT'] == "True" else {rules.emptyfile.output}
    output:
        tigmap     = "7-consensus/packages.tigName_to_ID.map",
        finished   = "7-consensus/packages.finished",
        packreport = "7-consensus/packages.report"
    params:
        minread   = config['cor_min_read'],
        ont_subset = lambda wildcards: getBuildPackagesInputONT(wildcards, config["withONT"], config["withBAM"]),
    log:
        err        = '7-consensus/buildPackages.err'
    threads:
        int(config['par_n_cpus'])
    resources:
        job_id = 1,
        n_cpus = int(config['par_n_cpus']),
        mem_gb = lambda wildcards, input, attempt: getBuildPackageMemoryRequest(attempt),
        time_h = lambda wildcards, input, attempt: getTimeRequest(attempt, 'par')
    shell:
        '''
cd 7-consensus

cat > ./buildPackages.sh <<EOF
#!/bin/sh
set -e

mkdir -p packages

rm -rf ./packages.seqStore
{VERKKO}/bin/sqStoreCreate \\\\
  -o packages.seqStore \\\\
  -minlength {params.minread} \\\\
  -raw -nanopore raw {params.ont_subset} {input.hifi}

{VERKKO}/bin/layoutToPackage \\\\
    -layout ../{input.layout} \\\\
    -output packages/part###.cnspack \\\\
    -idmap  packages \\\\
    -partition 0.8 1.5 0.04 \\\\
    -store packages.seqStore \\\\
  > ../{output.packreport} \\\\
&& \\\\
rm -rf ./packages.seqStore \\\\
&& \\\\
touch ../{output.finished}
EOF

chmod +x ./buildPackages.sh

./buildPackages.sh > ../{log.err} 2>&1
        '''

