#############################################################################
 #
 #  This file is part of Verkko, a software program that assembles
 #  whole-genome sequencing reads into telomere-to-telomere
 #  haplotype-resolved chromosomes.
 #
 #  Except as indicated otherwise, this is a 'United States Government
 #  Work', and is released in the public domain.
 #
 #  File 'README.licenses' in the root directory of this distribution
 #  contains full conditions and disclaimers.
 #
 ##

#
#  Rule untip ....
#
#  This is postprocess in the original.
#    postprocess.sh \
#      unitig-unrolled-ont-resolved.gfa \              <- processONT.output.graph
#      unitig-popped-unitig-normal-connected-tip.gfa   <- verkko.input.graph
#
#  Configuration Parameters:
#    ...
#

rule untip:
    input:
        graph  = rules.processONT.output.graph if config['withONT'] == "True" else rules.processGraph.output.gfa,
        hificov= rules.buildGraph.output.hificov
    output:
        graph  = rules.verkko.input.graph,
        hificov= "5-untip/nodecov_hifi_fix.csv"
    log:
        err    = '5-untip/untip.err'
    params:
        maps   = getAllMappings(5),
        graphs = getAllGraphs(5),
        aligns = getAllAlignments(5)
    threads:
        int(config['utp_n_cpus'])
    resources:
        job_id = 1,
        n_cpus = int(config['utp_n_cpus']),
        mem_gb = lambda wildcards, input, attempt: getMemoryRequest(attempt, 'utp'),
        time_h = lambda wildcards, input, attempt: getTimeRequest(attempt, 'utp')
    shell:
        '''
cd 5-untip

cat > ./untip.sh <<EOF
#!/bin/sh
set -e

echo UntipRelative
{PYTHON} {VERKKO}/scripts/untip_relative.py 35000 35000 0.1 0.1 \\\\
< ../{input.graph} \\\\
> connected-tip.gfa


echo Unitigify 3
{PYTHON} {VERKKO}/scripts/unitigify.py utig3- unitig-mapping-3.txt \\\\
< connected-tip.gfa \\\\
> unitig-normal-connected-tip.gfa


echo Combine mappings
cat {params.maps} unitig-mapping-3.txt \\\\
> combined-nodemap-1.txt

echo Combine edges
cat {params.graphs} connected-tip.gfa unitig-normal-connected-tip.gfa \\\\
| grep '^L' \\\\
> combined-edges-1.gfa


# this used to have grep -v \*
#
echo Find lengths
cat {params.graphs} connected-tip.gfa unitig-normal-connected-tip.gfa \\\\
| awk 'BEGIN \\\\
       {{ \\\\
          FS="[ \\t]+"; OFS="\\t"; \\\\
       }} \\\\
       \$1 == "S" \\\\
       {{ \\\\
           print \$2, length(\$3); \\\\
       }}' \\\\
> nodelens-1.txt

echo Fix coverage
{PYTHON} {VERKKO}/scripts/get_original_coverage.py \\\\
  ../5-untip/unitig-normal-connected-tip.gfa \\\\
  combined-nodemap-1.txt \\\\
  combined-edges-1.gfa \\\\
  nodelens-1.txt \\\\
  ../{input.hificov} \\\\
> ../{output.hificov}

echo Pop bubbles based on coverage
{PYTHON} {VERKKO}/scripts/pop_bubbles_coverage_based.py \\\\
  ../{output.hificov} \\\\
< ../5-untip/unitig-normal-connected-tip.gfa \\\\
> popped-unitig-normal-connected-tip.gfa \\\\
2> popinfo.err

echo Unroll simple loops
{PYTHON} {VERKKO}/scripts/unroll_simple_loops.py popped-unitig-normal-connected-tip.gfa ../{output.hificov} > unrolled-popped-unitig-normal-connected-tip.gfa 2>> unitig-mapping-3.txt

echo Unitigify 4
{PYTHON} {VERKKO}/scripts/unitigify.py utig4- unitig-mapping-4.txt \\\\
< unrolled-popped-unitig-normal-connected-tip.gfa \\\\
> pregap-popped-unitig-normal-connected-tip.gfa

echo Add homozygous node scaffold edges
cat combined-nodemap-1.txt unitig-mapping-3.txt unitig-mapping-4.txt > combined-nodemap-2.txt
{PYTHON} {VERKKO}/scripts/add_hom_node_scaffold_edges.py pregap-popped-unitig-normal-connected-tip.gfa combined-nodemap-2.txt homgap > ../{output.graph}

EOF

chmod +x ./untip.sh

./untip.sh > ../{log.err} 2>&1
        '''
