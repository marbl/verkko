#############################################################################
 #
 #  This file is part of Verkko, a software program that assembles
 #  whole-genome sequencing reads into telomere-to-telomere
 #  haplotype-resolved chromosomes.
 #
 #  Except as indicated otherwise, this is a 'United States Government
 #  Work', and is released in the public domain.
 #
 #  File 'README.licenses' in the root directory of this distribution
 #  contains full conditions and disclaimers.
 #
 ##

#
#  Rule generateConsensus computes consensus sequence for each contig.
#
#  Configuration Parameters:
#    ...
#
#  input.tigmap isn't used directly.  It serves as a sentinel that
#  the buildPackages step finished completely.

rule generateConsensus:
    input:
        package   = '7-consensus/packages/part{nnnn}.cnspack',
        tigmap    = rules.buildPackages.output.tigmap
    output:
        consensus = '7-consensus/packages/part{nnnn}.fasta'
    log:
        err       = '7-consensus/packages/part{nnnn}.err'
    params:
        jobid     = '{nnnn}'
    threads:
        int(config['cns_n_cpus']),
    resources:
        n_cpus = config['cns_n_cpus'],
        mem_gb = lambda wildcards, input, attempt: getMemoryRequest(attempt, 'cns'),
        time_h = lambda wildcards, input, attempt: getTimeRequest(attempt, 'cns')
    shell:
        '''
cd 7-consensus

mkdir -p packages

cat > ./packages/part{wildcards.nnnn}.sh <<EOF
#!/bin/sh
set -e

{VERKKO}/bin/utgcns \\\\
    -V -V -V \\\\
    -threads {threads} \\\\
    -import ../{input.package} \\\\
    -A ../{output.consensus} \\\\
    -C 2 -norealign \\\\
    -maxcoverage 50 \\\\
    -e 0.20 \\\\
    -l 2500 \\\\
    -pbdagcon \\\\
    -edlib
EOF

chmod +x ./packages/part{wildcards.nnnn}.sh

./packages/part{wildcards.nnnn}.sh > ../{log.err} 2>&1
        '''
